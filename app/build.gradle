plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dev.zacsweers.moshix' version '0.17.1'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
    id "com.google.protobuf" version "0.8.18"
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "bruhcollective.itaysonlab.jetispot"
        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "1.0"
        resConfigs "en"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    sourceSets.main {
        jniLibs.srcDir 'src/main/libs'
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
        resources {
            excludes += "/META-INF/*.kotlin_module"
            excludes += "/META-INF/*.version"
            excludes += "/META-INF/**"
            excludes += "/kotlin/**"
            excludes += "/kotlinx/**"
            excludes += "**/*.properties"
            excludes += "DebugProbesKt.bin"
            excludes += "kotlin-tooling-metadata.json"
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            excludes += 'log4j2.xml'
            excludes += '**.proto'
        }
    }
}

moshi {
    // Opt-in to enable moshi-sealed, disabled by default.
    enableSealed.set(true)
}

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-guava:1.6.1'

    implementation 'androidx.core:core-ktx:1.7.0'

    implementation "androidx.datastore:datastore:1.0.0"
    implementation  "com.google.protobuf:protobuf-java:3.20.0"

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.material3:material3:1.0.0-alpha09"
    implementation "androidx.navigation:navigation-compose:2.4.2"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    //implementation "androidx.media3:media3-common:$media3_version"
    //implementation "androidx.media3:media3-session:$media3_version"

    implementation "io.coil-kt:coil-compose:2.0.0-rc03"
    implementation 'com.google.accompanist:accompanist-coil:0.15.0'
    implementation 'com.google.accompanist:accompanist-systemuicontroller:0.24.6-alpha'

    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation 'androidx.activity:activity-compose:1.4.0'

    implementation('xyz.gianlu.librespot:librespot-player:1.6.2:thin') {
        exclude group: 'xyz.gianlu.librespot', module: 'librespot-sink'
        exclude group: 'com.lmax', module: 'disruptor'
        exclude group: 'org.apache.logging.log4j'
        //exclude group: 'com.google.protobuf'
    }

    implementation 'uk.uuid.slf4j:slf4j-android:1.7.30-0'

    implementation("com.squareup.moshi:moshi-kotlin:1.13.0")

    implementation("androidx.hilt:hilt-navigation-compose:1.0.0")
    implementation "com.google.dagger:hilt-android:2.41"
    kapt "com.google.dagger:hilt-compiler:2.41"

    // Interacting with MediaSessions
    implementation "androidx.media2:media2-session:$media2_version"
    // optional - Implementation of a SessionPlayer
    implementation "androidx.media2:media2-player:$media2_version"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.20.0"
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    //option 'lite'
                }
            }
        }
    }
}